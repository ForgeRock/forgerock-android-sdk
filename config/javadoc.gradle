/*
 * Copyright (c) 2021 ForgeRock. All rights reserved.
 *
 * This software may be modified and distributed under the terms
 * of the MIT license. See the LICENSE file for details.
 */

apply plugin: 'org.jetbrains.dokka'


/**
 * Generate Javadoc, delombok then generate Javadoc
 */
configurations {
    delombok
}

task delombok {
    def srcJava = 'src/main/java'
    doLast {
        ant.echo('Delomboking...')
        ant.taskdef(name: 'delombok',
                classname: 'lombok.delombok.ant.Tasks$Delombok',
                classpath: configurations.delombok.asPath)
        ant.delombok(verbose: 'true', from: srcJava, to: "$buildDir/src-delomboked")
    }
}

dokkaJavadoc.dependsOn delombok
dokkaHtml.dependsOn delombok

dokkaJavadoc {
    dokkaSourceSets {
        named("main") {
            // Main source set conf
            outputDirectory = file("build/javadoc/$project.name-dokka")
            sourceRoots.setFrom(file("$buildDir/src-delomboked"))
        }
    }
}

dokkaHtml {
    dokkaSourceSets {
        named("main") {
            // Main source set conf
            outputDirectory = file("build/html/$project.name-dokka")
            sourceRoots.setFrom(file("$buildDir/src-delomboked"))
        }
    }
}

task sourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from android.sourceSets.main.java.srcDirs
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    archiveClassifier.set("javadoc")
    from new File("build/javadoc/$project.name-dokka")
}

artifacts {
    archives sourcesJar
    archives javadocJar
}