/*
 * Copyright (c) 2019 - 2020 ForgeRock. All rights reserved.
 *
 * This software may be modified and distributed under the terms
 * of the MIT license. See the LICENSE file for details.
 */

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: "com.adarshr.test-logger"


android {
    compileSdkVersion 30

    useLibrary 'android.test.base'
    useLibrary 'android.test.mock'

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 5
        versionName "2.2.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [
                    'appAuthRedirectScheme': 'org.forgerock.demo'
            ]
        }

        debug {
            manifestPlaceholders = [
                    'appAuthRedirectScheme': 'org.forgerock.demo'
            ]
        }
    }

    /* Comment this to debug instrument Test,
    * There is an issue for AS to run NDK with intrument Test */
    externalNativeBuild {
        ndkBuild {
            path 'src/main/jni/Android.mk'
        }
    }

    testOptions {
        unitTests.includeAndroidResources = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        pickFirst '**/*.so'
    }

    testOptions {
        unitTests.all {
            exclude '**/*TestSuite*'
        }
    }

}
/**
 * Generate Javadoc, delombok then generate Javadoc
 */
configurations {
    delombok
}

task delombok {
    def srcJava = 'src/main/java'
    doLast {
        ant.echo('Delomboking...')
        ant.taskdef(name: 'delombok',
                classname: 'lombok.delombok.ant.Tasks$Delombok',
                classpath: configurations.delombok.asPath)
        ant.delombok(verbose: 'true', from: srcJava, to: "$buildDir/src-delomboked")
    }
}

task javadoc(type: Javadoc, dependsOn: delombok) {
    source = "$buildDir/src-delomboked"
    destinationDir = new File("$buildDir/generated-javadoc")
    failOnError = false
    ext.androidDoc = "${android.sdkDirectory}/docs/reference"
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    options.linksOffline("http://d.android.com/reference", ext.androidDoc)
    options.links("http://docs.oracle.com/javase/7/docs/api/")
    options.links("http://d.android.com/reference/")
}

/**
 * JCenter Dependency Manager
 */

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

group 'org.forgerock'

project.version = System.getProperty("bintray.version")

bintray {
    user = System.getProperty("bintray.user")
    key = System.getProperty("bintray.apiKey")

    configurations = ['archives']

    pkg {
        repo = 'maven'
        name = 'forgerock-auth'
        userOrg = 'forgerock-sdk'
        desc = 'ForgeRock SDK is a toolkit that allows developers communicate efficiently with ForgeRock OpenAM and ForgeRock Identity Cloud.'
        licenses = ['MIT']
        labels = ['aar', 'android', 'oauth', 'security', 'forgerock']
        publicDownloadNumbers = true
        vcsUrl = gitUrl
        issueTrackerUrl = issueUrl
        websiteUrl = siteUrl
        version {
            name = project.version
            desc = 'Forgerock Android SDK'
            released = new Date()
        }
    }
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'Forgerock Auth SDK'
                url siteUrl
                licenses {
                    license {
                        name 'MIT'
                        url 'https://opensource.org/licenses/MIT'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl

                }
            }
        }
    }
}


testlogger {
    theme 'mocha'
    showExceptions true
    showStackTraces true
    showFullStackTraces false
    showCauses true
    slowThreshold 2000
    showSummary true
    showSimpleNames false
    showPassed true
    showSkipped true
    showFailed true
    showStandardStreams false
    showPassedStandardStreams true
    showSkippedStandardStreams true
    showFailedStandardStreams true
}


/**
 * Dependencies
 */
dependencies {
    api project(':forgerock-core')
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.squareup.okhttp3:okhttp:4.8.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.8.0'
    implementation 'androidx.annotation:annotation:1.1.0'

    //Make it optional for developer
    compileOnly 'com.google.android.gms:play-services-location:17.1.0'
    compileOnly 'com.google.android.gms:play-services-safetynet:17.0.0'
    compileOnly 'net.openid:appauth:0.7.1'

    testImplementation 'androidx.test:core:1.3.0'
    testImplementation 'androidx.test:runner:1.3.0'

    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'com.squareup.okhttp:mockwebserver:2.7.5'
    androidTestImplementation 'commons-io:commons-io:2.6'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'com.google.android.gms:play-services-location:17.1.0'
    //Do not update to the latest library, Only 2.x compatible with Android M and below.
    androidTestImplementation 'org.assertj:assertj-core:2.9.1'

    testImplementation 'junit:junit:4.13'
    testImplementation 'org.robolectric:robolectric:4.3.1'
    testImplementation 'com.squareup.okhttp:mockwebserver:2.7.5'
    testImplementation 'commons-io:commons-io:2.6'
    testImplementation 'org.assertj:assertj-core:3.18.1'
    testImplementation "androidx.fragment:fragment-testing:1.2.5"
    testImplementation "androidx.test.espresso:espresso-intents:3.3.0"
    testImplementation 'net.openid:appauth:0.7.1'

    testImplementation 'org.mockito:mockito-core:3.6.0'
    testImplementation 'org.powermock:powermock-module-junit4:2.0.2'
    testImplementation 'org.powermock:powermock-api-mockito2:2.0.2'


    compileOnly "org.projectlombok:lombok:1.18.8"
    delombok "org.projectlombok:lombok:1.18.8"
    annotationProcessor 'org.projectlombok:lombok:1.18.8'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

}
repositories {
    mavenCentral()

}


